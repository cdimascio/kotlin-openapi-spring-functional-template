buildscript {
    ext {
        kotlinVersion = '1.3.21'
        springBootVersion = '2.1.0.RELEASE'
        distName = "kotlin-swagger-spring-functional-template"
        distVersion = '1.4.0'
    }
    repositories {
        jcenter()
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.junit.platform:junit-platform-gradle-plugin:1.0.2")
        classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2")
    }
}

apply plugin: 'kotlin'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'application'
apply plugin: "jacoco"
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: "com.github.kt3k.coveralls"

mainClassName = 'functional.ApplicationKt'

version = distVersion
sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjsr305=strict"]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjsr305=strict"]
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

configurations {
    ktlint
}

jar {
    manifest {
        attributes 'Main-Class': 'functional.ApplicationKt'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") {
            bomProperty("kotlin.version", "${kotlinVersion}")
        }
    }
}
dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib")
    implementation("org.jetbrains.kotlin:kotlin-reflect")

    implementation("org.springframework:spring-webflux")
    implementation("org.springframework:spring-context")
    implementation("org.springframework.boot:spring-boot-starter-webflux")

    implementation 'io.projectreactor.netty:reactor-netty'

    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

    implementation 'io.github.cdimascio:java-dotenv:3.1.3'
    implementation 'io.github.cdimascio:openapi-spring-webflux-validator:1.0.1'

    implementation("org.slf4j:slf4j-api")
    implementation("ch.qos.logback:logback-classic")

    runtime files('src/main/resources')

    testImplementation('io.projectreactor:reactor-test')
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testRuntime("org.junit.jupiter:junit-jupiter-engine")

    ktlint "com.github.shyiko:ktlint:0.29.0"
}


task unzip(type: Copy) {
    def zipFile = file("build/distributions/${distName}-${distVersion}.zip")
    def outputDir = file("${buildDir}/unpacked/dist")

    from zipTree(zipFile)
    into outputDir
}

task lintKotlin(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn lintKotlin

task formatKotlin(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}